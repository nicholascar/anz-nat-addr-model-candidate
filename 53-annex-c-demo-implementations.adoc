== Annex C: Demo Implementations

Several demonstration implementations of this Address model are given in the code repository containing its content.

=== C.1 RDF Database Demo

The 15 or so extended examples data files within the code repository (see above Annex for details) represent an implementation of this model for they are formulated in RDF data and are thus able to be loaded into an RDF database (often called a "triplestore") and queried.

To demonstrate this, the multiple example files can be loaded directly into such tools as Apache's https://jena.apache.org/documentation/fuseki2/[Fuseki] database. They can also be combined into one file using RDF tooling and that uploaded. The script `_compound_example.py` in the examples directory compounds all the examples into the single file https://github.com/nicholascar/anz-nat-addr-model-candidate/blob/main/extended-examples/_all.ttl[_all.ttl].

Once loaded into a triplestore, https://www.w3.org/TR/sparql11-query/[SPARQL] queries can be used to interrogate the data similarly to how SQL queries are used for relational databases. Some example SPARQL queries for the examples data are:

==== C.1.1 Ordered Address Components

For those Addresses that have components - not all in the example data - this query orders the components as per Queensland norms, including the new Property Name type component at the start.

```sparql
PREFIX addr: <https://w3id.org/profile/anz-address/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX actiso: <http://def.isotc211.org/iso19160/-1/2015/Address/code/AddressComponentType/>

SELECT ?a ?ac_type ?val ?val_text
WHERE {
    VALUES (?ac_type ?ac_order) {
        (actiso:buildingName 1)
        (actiso:propertyName 2)
        (actiso:indigenousPlaceName 3)
        (actiso:flatTypeCode 4)
        (actiso:flatNumber 5)
        (actiso:numberFirst 6)
        (actiso:streetLocality 7)
        (actiso:locality 8)
        (actiso:postcode 9)
        (actiso:administrativeArea 10)
    }
    ?a a addr:Address ;
        addr:hasAddressComponent [
            dcterms:type ?ac_type ;
            addr:hasValue ?val ;
            addr:hasValueText ?val_text
        ]
}
ORDER BY ?a ?ac_order
```

This query selects only Address Components of the types given in the `VALUES` list and orders them as per the integer numbering given. Results for the example address 2/18 Oxford Place (see extended-examples/oxford-place.ttl) are:

|===
| ?a | ?ac_type | ?val | ?val_text

|`a:GAQLD161268186` | `actiso:flatNumber` | "2"^^xsd:integer | "2"
|`a:GAQLD161268186` | `actiso:numberFirst` | "18"^^xsd:integer | "18"
|`a:GAQLD161268186` | `actiso:streetLocality` | `sl:QLD140492` | "Oxford Place"
|`a:GAQLD161268186` | `actiso:locality` | `l:loc38f189794e03` | "Shorncliffe"
|===

* `l` : `https://linked.data.gov.au/dataset/gnaf/locality/`
* `sl` : `https://linked.data.gov.au/dataset/gnaf/streetLocality/`

==== C.1.2 Selecting addresses by geocode type and in an area

The following query selects addresses from the examples that have a Geocode of type Property Access Point (PAP) and that are within the Bounding Box of 154 -27, 154 -28, 153 -28, 153 -27, 154 -27.

```sparql
PREFIX addr: <https://w3id.org/profile/anz-address/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX gct: <https://linked.data.gov.au/dataset/gnaf/code/geocodeType/>

SELECT ?a ?wkt
WHERE {
    ?a a addr:Address ;
        addr:hasGeocode [
            dcterms:type gct:PAP ;
            geo:asWKT ?wkt ;
        ] .

    FILTER (geof:sfWithin(?wkt, "POLYGON ((154 -27, 154 -28, 153 -28, 153 -27, 154 -27))"))
}
```

The results from this query are:

|===
| ?a | ?wkt

| `a:GAQLD161337673` | "POINT(153.157220 -27.624450)"
| `a:GAQLD155231408` | "POINT(153.157220 -27.624450)"
| `a:GAQLD161294216` | "POINT(153.045546 -27.341745)"
|===

Two of the results have the same geocode location. This is due to `a:GAQLD155231408` being a primary address with `a:GAQLD161337673` being a secondary and sharing a Property Access Point location (geocode).

Without the spatial bounding box filter, we would also get one more result:

|===
| ?a | ?wkt

| `a:GAQLD157959170` | "POINT(151.95179638 -28.82625239)"
|===

Removing the geocode type filter, we get all of the Addresses that have Geocodes, and the Geocodes' values, in the extended examples: there are 19.

So SPARQL queries can filter results on type and also using spatial relations, such as _within_ a given area.

==== C.1.3 Selecting Addresses in relation to an object

SPARQL queries can select Addresses based on Address Component values which may either be literal values (numbers/strings) or complex objects.

To get all the example Addresses within the Queensland locality of Shorncliffe:

```sparql
PREFIX addr: <https://w3id.org/profile/anz-address/>
PREFIX loc: <https://linked.data.gov.au/dataset/gnaf/locality/>

SELECT ?a ?val ?val_text
WHERE {
  ?a addr:hasAddressComponent/addr:hasValue loc:loc38f189794e03
}
```

This query finds two results (the oxford-place examples) since they indicate they have Shorncliffe as an Address Component of type `actiso:locality` by reference to the GNAF Locality PID `<https://linked.data.gov.au/dataset/gnaf/locality/loc38f189794e03>`.

To find all of the Addresses in Queensland we could use the PID of Queensland given by the https://asgs.linked.fsdf.org.au/[Australian Statistical Geography Standard] since that's the object ID related to in this examples data

```sparql
PREFIX addr: <https://w3id.org/profile/anz-address/>
PREFIX loc: <https://linked.data.gov.au/dataset/gnaf/locality/>

SELECT ?a ?val ?val_text
WHERE {
  ?a addr:hasAddressComponent/addr:hasValue <https://linked.data.gov.au/dataset/asgsed3/STE/3>
}
```

Note that the PID for Queensland is external to this model but resolves, online to the ASGS Queensland object:

* https://linked.data.gov.au/dataset/asgsed3/STE/3


=== C.2 Relational Database Demo

The 15 or so extended examples within the code repository (see above Annex for details) have been stored in an https://www.sqlite.org[SQLite] Database also within this code repository.

[NOTE]
To be fully functional, the https://www.gaia-gis.it/fossil/libspatialite/index[SpatiaLite] extension to SQLite need to be enabled so spatial functions may be performed. This extension is _NOT_ enabled for this demonstration database to keep the geometry data human-readable (text) rather than implementing it as binary data types.

* https://github.com/nicholascar/anz-nat-addr-model-candidate/tree/main/implementations/relational/qld-scenarios[Source SQL files and loaded .sqlite database]

The database consists of 21 tables that together store:

* the primary data of the examples
** the Addresses in table _addresses_, Address Components in _address_components_ etc.
* the codelists/vocabularies listed in <<Supporting Vocabularies>>
** as lookups
* an IRI prefixes table
** mapping short form IRIs to complete IRIs
** as per <<Namespaces>>

These tables fully represent the content of the examples and the database schema used is therefore a complete relational implementation of this model.

To show equivalence to the <<C.1 RDF Database Demo, RDF Database Demo>>, the three SPARQL queries shown there are reimplemented as SQLite SQL queries next.

Additionally, <<C.2.4 Converting relational to RDF data>> demonstrates how to convert some of the content from this relational implementation to RDF so it may be validated using the validators in <<Annex A: Validators>>.

==== C.2.1 Ordered Address Components

The following query yields identical results to that in <<C.1.1 Ordered Address Components>>:

```sql
SELECT *
FROM addresses_components
ORDER BY
  address_pid,
  CASE type
    WHEN 'actiso:buildingName' THEN 0
    WHEN 'actiso:propertyName' THEN 1
    WHEN 'actiso:indigenousPlaceName' THEN 2
    WHEN 'actiso:flatTypeCode' THEN 3
    WHEN 'actiso:flatNumber' THEN 4
    WHEN 'actiso:numberFirst' THEN 5
    WHEN 'actiso:streetLocality' THEN 6
    WHEN 'actiso:locality' THEN 7
    WHEN 'actiso:postcode' THEN 8
    WHEN 'actiso:administrativeArea' THEN 9
  END
```

==== C.2.2 Selecting addresses by geocode type and in an area

To make an SQL query equivalence to the sPARQL query in <<C.1.2 Selecting addresses by geocode type and in an area>>, an SQL join between the _addresses_ and _geocodes_ tables must be performed:

```sql
SELECT pid, geometry
FROM addresses
JOIN geocodes
ON addresses.pid = geocodes.address_pid
WHERE type = 'gt:PAP'
AND MBRWithin(
        GeomFromText('POLYGON ((154 -27, 154 -28, 153 -28, 153 -27, 154 -27))'),
        geometry
    );
```

Results from this query are the same as those in <<C.1.2 Selecting addresses by geocode type and in an area>>.

NOTE: For true operations, the function `MBRWithin` requires a `GEOMETRY` data field to operate on, not text as this example `geometry` field is. This example uses text so the values can be read. Removing the `MBRWithin` clause from the query above results in 4 results from the SQLite database, as per the reduces SPARQL query example above.

==== C.2.3 Selecting Addresses in relation to an object

The following SQL query performs equivalently to the first query in <<C.1.3 Selecting Addresses in relation to an object>>.

```sql
SELECT address_pid, has_value, has_value_text
FROM addresses_components
WHERE has_value = 'loc:loc38f189794e03'
```
Results for this query are the same as the equivalent SPARQL query.

NOTE: To match on a full IRI for 'loc:loc38f189794e03', which the equivalent SPARQL query above does with use of the prefix statement `PREFIX loc: <https://linked.data.gov.au/dataset/gnaf/locality/>`, the SQLite table _pid_prefixes_ can be consulted to interpret 'loc:' in the query below.

==== C.2.4 Converting relational to RDF data

Since content according to this model can only be canonically represented in RDF, it is necessary that all other forms of content claiming conformance to this model show they can be converted to RDF for canonical data validation using the validators in <<Annex A: Validators>>.

The following SQL query obtains all the information necessary to formulate RDF for the valid example address `<http://example.com/oxford>` in the file extended-examples/oxford-place.ttl:

```sql
SELECT
    a.pid, a.is_address_for,
    ac.type, ac.has_value, ac.has_value_text,
    g.geometry, g.type,
    aa.address_pid
FROM addresses a
JOIN addresses_components ac
ON a.pid = ac.address_pid
JOIN geocodes g
ON a.pid = g.address_pid
JOIN address_aliases aa
ON a.pid = aa.alias_pid
WHERE a.pid = 'ex:oxford';
```

In the query above, a 4-table join is performed to obtain Address, Address Component, Address Geocode and Address Alias data.

A simple string templating function in a programming language such as Python may be used to convert those results into RDF or an RDF toolkit, such as Python's https://pypi.org/project/rdflib/[RDFLib] or Java's https://jena.apache.org/[Jena], may be used.

The file https://github.com/nicholascar/anz-nat-addr-model-candidate/tree/main/implementations/relational/qld-scenarios/rdf_converter.py[rdf_converter.py] is a Python script that:

* reads the Relational Database Demo file
* utilises the _pid_prefixes_ to map prefixed data to full IRIs
* extracts all the elements of the `<http://example.com/oxford>` using the query above
* adds all elements ton an RDF graph
* prints the graph to text

The resultant graph is valid according to both of the validators files in <<Annex A: Validators>>, just as the original in the extended-examples folder is.

=== C.3: G-NAF for Queensland Demo

The entire contents of the Geocoded National Address File (G-NAF) for Queensland have been converted into this model's relational form as per the SQLite implementation described above in <<C.2 Relational Database Demo>>.

This complete conversion demonstrates this model's ability to represent the entire content of the G-NAF for Queensland but it is too large to include in this model's code repository. It may be obtained on request from this model's author.
